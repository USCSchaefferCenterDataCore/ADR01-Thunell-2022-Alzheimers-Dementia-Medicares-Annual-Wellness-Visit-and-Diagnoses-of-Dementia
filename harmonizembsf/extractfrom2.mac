/* get a list of variables from the claims files
   fn is input file name
   year is year of claims
   typ is type of claim file, e.g., ip/op/car
   inlist is a list of variables from the input file that will be renamed
   outlist is the list of variable names to which inlist vars will be renamed in extract file
   asislist is a list of variables that will be extracted as is 
   inlib is libname for claims file
   olib is libname for extract file
   ofn is the sas file name for extract file
   idv is beneficiary id
   claimid is claim id variable
   CRL is last letter of typ, e.g., C for claims files, R for revctr, L for line item
   RLid is revctr/line-item id variable
   
   Need to add procvarlist option or means to include it.
   Also add do crosswalk if idv is not bene_id.
 - extract step-output apchipps as apc on op, hipps_prev on hha.
                    output hcpcs_cd as hipps on snf,hha,ip when rev center is 0023-0025
                    keep only hcpcs_cd when it is an hcpcs_cd.
*/
%macro extractfrom2(fn,year,typ,incfn=,
                inlist=,outlist=,asislist=,
                inlib=,olib=&outlib,ofn=&outfn,
                ofn2=,inlist2=,outlist2=,asislist2=,
                idv=bene_id,claimid=clm_id,CRL=C,RLid=,dtlist=);

title2 &year &typ from &fn to &ofn;

%if &CRL ne C %then %let clm_lnrev=clm_ln;

data &olib..&typ._&ofn  
     (keep=&idv year claim_id &clm_lnrev &claimid &RLid claim_type &dtlist  &outlist &asislist fromfile)

%if %length(&ofn2)>0 %then %do;
     &olib..&typ._&ofn2
     (keep=&idv year claim_id &clm_lnrev &claimid &RLid claim_type &dtlist &outlist2 &asislist2 fromfile)
%end;

     ; /* end data statement */
   
   set &inlib..&fn (keep=&idv &claimid &RLid &inlist &inlist2 &asislist); 

   rename %renv(&inlist &inlist2,&outlist &outlist2);
   
   length claim_type $ 4 claim_id $ 15;
   length fromfile $ 20;
   length year 3;
   length datelist $ 500;
   
   fromfile=lowcase("&fn"); /* save source file name */
   
   claim_type=upcase("&typ&CRL"); /* save source claim type (e.g., ip, snf, car) */
   year=&year;
   
   /* common variable name is claim_id. 
      If numeric make it a 15-char left-justified var 
   */
   if vtype(&claimid)="N" then claim_id=left(put(&claimid,15.0));
   else claim_id=&claimid;
   
   label claim_id="Claim ID-char 15 (from &claimid)"
         claim_type="Source file type (e.g., IP,OP,CAR)"
         fromfile="Source file name"
         ;
   
   %if &CRL ne C %then %do;
       %if %upcase(&RLid) = CLM_LN %then %do;
           label clm_ln = "Claim detail ID -revctr or line";
       %end;
       %else %do;
          clm_ln = &RLid;
          label clm_ln = "Claim detail ID -revctr or line (from &RLid)";
       %end;
   %end;

   /* add old varname to label for new varname
      and make all dates have date9. format */
   if _N_=1 then do;
  
       datelist="";

   %let datelist=;
   %let nv=1;
   %let nxtv=%scan(&inlist,&nv);
   %do %while (%length(&nxtv)>0);

       varlabel=vlabel(&nxtv) || "(from &nxtv)";
       call symput("%scan(&outlist,&nv)",varlabel);

       if index(vformat(&nxtv),"YYMMDD")>0 then 
          datelist=trim(left(datelist)) || " %scan(&outlist,&nv)";
       
       %let nv=%eval(&nv+1);
       %let nxtv=%scan(&inlist,&nv);
   %end;
   
   %let nv=1;
   %let nxtv=%scan(&asislist,&nv);

   %do %while (%length(&nxtv)>0);

       %if %index(&nxtv,%str(:))=0 %then %do;
           if index(vformat(&nxtv),"YYMMDD")>0 then 
              datelist=trim(left(datelist)) || " &nxtv";
       %end;

       %let nv=%eval(&nv+1);
       %let nxtv=%scan(&asislist,&nv);
   %end;
      
       call symput("datelist",trim(left(datelist))); /* output macro var with date vars */

   %if %length(&ofn2)>0 %then %do;
       %let nv=1;
       %let nxtv=%scan(&inlist2,&nv);
       %do %while (%length(&nxtv)>0);
           
           varlabel=vlabel(&nxtv) || "(from &nxtv)";
           call symput("%scan(&outlist2,&nv)",varlabel);
       
           %let nv=%eval(&nv+1);
           %let nxtv=%scan(&inlist2,&nv);
       %end;
   %end;
       
   end;

   %if %length(&incfn)>0 %then %do;
       /*** include sas code specific to &typ ***/
       %include "&incfn";
   %end;
   
run;

proc datasets lib=&olib;
   modify &typ._&ofn;
   /* relabel renamed variables to include old var name */
   %let nv=1;
   %let nxtv=%scan(&outlist,&nv);
   %do %while (%length(&nxtv)>0);
       
       label &nxtv = "&&&nxtv";
       
       %let nv=%eval(&nv+1);
       %let nxtv=%scan(&outlist,&nv);
   %end;

   /* format date variables uniformly */
   %if %length(%scan(&datelist,1))>0 %then %do;
       format &datelist date9.;
   %end;
   format &dtlist date9.;
   
   %if %length(&ofn2)>0 %then %do;
       modify &typ._&ofn2;
       /* relabel renamed variables to include old var name */
       %let nv=1;
       %let nxtv=%scan(&outlist2,&nv);
       %do %while (%length(&nxtv)>0);
           
           label &nxtv = "&&&nxtv";
           
           %let nv=%eval(&nv+1);
           %let nxtv=%scan(&outlist2,&nv);
       %end;

       /* format date variables uniformly */
       %if %length(%scan(&datelist,1))>0 %then %do;
           format &datelist date9.;
       %end;
       format &dtlist date9.;
   %end;
  run;

%mend extractfrom2;
