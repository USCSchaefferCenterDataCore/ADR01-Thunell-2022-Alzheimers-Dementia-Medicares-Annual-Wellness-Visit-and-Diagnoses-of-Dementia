/* 
	process all claim types in typlist for specified years.
  Handles 2002-2010
  Requires that nseg[typ][yyyy] macro variables be set up (include nseg.
*/
%macro extprocs2(typlist,orgnames,stdnames,asisnames=,
                 outf2=,inv2=,outv2=,asisv2=,codein=,
                 dts=,
                 begy=2002,endy=2008,
                 smp=20_,clms=_clms,revlin=,
                 idvar=bene_id,claimvar=clm_id,RLvar=,typsfx=C);

   %* process each claim type in typlist  *;
   
   %let nt=1;
   %let nxtyp=%scan(&typlist,&nt);
   %do %while (%length(&nxtyp)>0);
       
       %if %substr(&nxtyp,1,2)=dm %then %let typlib=dme;
       %else %let typlib=&nxtyp;
       
       %do y=&begy %to &endy;
         %put nseg=&&&nseg&nxtyp&y;
         %if &&&nseg&nxtyp&y > 0 %then %do n=1 %to &&&nseg&nxtyp&y;
           %let fname=&nxtyp&smp&y&clms&n;
           %extractfrom2(&fname,&y,&nxtyp,
                     inlist=&orgnames,outlist=&stdnames,asislist=&asisnames,
                     inlib=&typlib,olib=work,ofn=&outfn.&y,
                     ofn2=&outf2.&y,inlist2=&inv2,outlist2=&outv2,asislist2=&asisv2,
                     incfn=&codein,
                     dtlist=&dts,
                     idv=&idvar,claimid=&claimvar,RLid=&RLvar,CRL=&typsfx);
           %if &n=1 %then %do;
              data &outlib..&nxtyp._&outfn.&y.;
                 set work.&nxtyp._&outfn.&y.;
                 run;
              %if %length(&outf2)>0 %then %do;
                  data &outlib..&nxtyp._&outf2.&y.;
                     set work.&nxtyp._&outf2.&y.;
                     run;
              %end;
           %end;
           %else %do;
              proc append base=&outlib..&nxtyp._&outfn.&y 
                          data=work.&nxtyp._&outfn.&y;
              %if %length(&outf2)>0 %then %do;
                  proc append base=&outlib..&nxtyp._&outf2.&y 
                              data=work.&nxtyp._&outf2.&y;
              %end;
           %end;

         %end;
         %else %do;
           %let fname=&nxtyp&smp&y&clms;
           %extractfrom2(&fname,&y,&nxtyp,
                     inlist=&orgnames,outlist=&stdnames,asislist=&asisnames,
                     inlib=&typlib,olib=&outlib,ofn=&outfn.&y,
                     ofn2=&outf2.&y,inlist2=&inv2,outlist2=&outv2,asislist2=&asisv2,
                     incfn=&codein,
                     dtlist=&dts,
                     idv=&idvar,claimid=&claimvar,RLid=&RLvar,CRL=&typsfx);
         %end;

        %*** if before 2006, we need to crosswalk ehic to bene_id 
             and add thru_dt  ***;
        %if &y < 2006 %then %do;
            %xwyr(&nxtyp._&outfn,&y,&y,lib=&outlib,renfn=Y,contlist=N);
        %end;
        
          proc sort data=&outlib..&nxtyp._&outfn&y;
             by bene_id claim_id &RLvar;
             run;
          %sascontents(&nxtyp._&outfn&y,lib=&outlib,contdir=&contentsdir,domeans=N)

          %if %length(&outf2)>0 %then %do;
              %*** if before 2006, we need to crosswalk ehic to bene_id 
                   ;
              %if &y < 2006 %then %do;
                  %xwyr(&nxtyp._&outf2,&y,&y,lib=&outlib,renfn=Y,contlist=N);
              %end;
              
              proc sort data=&outlib..&nxtyp._&outf2&y;
                 by bene_id claim_id &RLvar;
                 run;
              %sascontents(&nxtyp._&outf2&y,lib=&outlib,contdir=&contentsdir,domeans=N)
          %end;
       %end;
       
       %let nt=%eval(&nt+1);
       %let nxtyp=%scan(&typlist,&nt);
   %end;
%mend extprocs2;
