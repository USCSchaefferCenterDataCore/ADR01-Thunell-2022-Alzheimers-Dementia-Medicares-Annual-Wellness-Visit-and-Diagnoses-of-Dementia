/* 
	process all claim types in typlist for specified years.
  Handles 2002-2010
  Requires that nseg[typ][yyyy] macro variables be set up (include nseg.
*/
%macro extprocs1(typlist,orgnames,stdnames,asisnames=,begy=2002,endy=2008,
                 smp=20_,clms=_clms,revlin=,
                 idvar=bene_id,claimvar=clm_id,RLvar=,typsfx=C);

   %* process each claim type in typlist  *;
   
   %let nt=1;
   %let nxtyp=%scan(&typlist,&nt);
   %do %while (%length(&nxtyp)>0);
       
       %if %substr(&nxtyp,1,2)=dm %then %let typlib=dme;
       %else %let typlib=&nxtyp;
       
       %do y=&begy %to &endy;
         %put nseg=&&&nseg&nxtyp&y;
         %if &&&nseg&nxtyp&y > 0 %then %do n=1 %to &&&nseg&nxtyp&y;
           %let fname=&nxtyp&smp&y&clms&n;
           %extractfrom1(&fname,&y,&nxtyp,
                     inlist=&orgnames,outlist=&stdnames,asislist=&asisnames,
                     inlib=&typlib,olib=work,ofn=&outfn.&y,
                     idv=&idvar,claimid=&claimvar,RLid=&RLvar,CRL=&typsfx);
           %if &n=1 %then %do;
              data &outlib..&nxtyp._&outfn.&y.;
                 set work.&nxtyp._&outfn.&y.;
                 run;
           %end;
           %else %do;
              proc append base=&outlib..&nxtyp._&outfn.&y 
                          data=work.&nxtyp._&outfn.&y;
           %end;

         %end;
         %else %do;
           %let fname=&nxtyp&smp&y&clms;
           %extractfrom1(&fname,&y,&nxtyp,
                     inlist=&orgnames,outlist=&stdnames,asislist=&asisnames,
                     inlib=&typlib,olib=&outlib,ofn=&outfn.&y,
                     idv=&idvar,claimid=&claimvar,RLid=&RLvar,CRL=&typsfx);
         %end;

        %*** if before 2006, we need to crosswalk ehic to bene_id 
             and add thru_dt  ***;
        %if &y < 2006 %then %do;
            %xwyr(&nxtyp._&outfn,&y,&y,lib=procout,renfn=Y,contlist=N);
        %end;
        
          proc sort data=procout.&nxtyp._&outfn&y;
             by bene_id claim_id &RLvar;
             run;
          %sascontents(&nxtyp._&outfn&y,lib=procout,contdir=&contentsdir,domeans=N)

       %end;
       
       %let nt=%eval(&nt+1);
       %let nxtyp=%scan(&typlist,&nt);
   %end;
%mend extprocs1;
